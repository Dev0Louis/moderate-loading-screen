// Inspired in part by the buildscripts of FabricMC/fabric

plugins {
  id "fabric-loom" version "0.9.+"
  id "com.modrinth.minotaur" version "1.2.+"
  id "org.cadixdev.licenser" version "0.6.+"
  id "io.github.juuxel.loom-quiltflower" version "1.2.+"
}

String mod_version = "2.2.2-SNAPSHOT"

String minecraft_version = "1.17.1"   // https://fabricmc.net/versions.html
String yarn_mappings = "43"           // https://fabricmc.net/versions.html
String loader_version = "0.11.6"      // https://fabricmc.net/versions.html
String fabric_version = "0.38.2+1.17" // https://fabricmc.net/versions.html
String modmenu_version = "2.0.5"      // https://modrinth.com/mod/modmenu/versions

archivesBaseName = "quilt-loading-screen"
version = mod_version + "+" + minecraft_version

repositories {
  maven { url "https://maven.quiltmc.org/repository/release/" }
  maven { url "https://maven.terraformersmc.com" }
}

dependencies {
  minecraft "com.mojang:minecraft:${minecraft_version}"
  mappings "net.fabricmc:yarn:${minecraft_version}+build.${yarn_mappings}:v2"
  modImplementation "net.fabricmc:fabric-loader:${loader_version}"
  include(modImplementation(fabricApi.module("fabric-resource-loader-v0", fabric_version)))

  modImplementation "com.terraformersmc:modmenu:${modmenu_version}"
}

processResources {
  inputs.property "version", project.version
  filesMatching("fabric.mod.json") {
    expand "version": project.version
  }
}

tasks.withType(JavaCompile) { it.options.encoding = "UTF-8" }

jar { from("LICENSE") }

task modrinth(type: com.modrinth.minotaur.TaskModrinthUpload) {
  onlyIf { System.getenv("MODRINTH") }
  token = System.getenv("MODRINTH")
  projectId = "VPU6VYVP"
  versionNumber = mod_version
  versionType = "RELEASE"
  uploadFile = remapJar
  addGameVersion minecraft_version
  addLoader "fabric"
}
